macroscript httpApiPublicUrl(
	/** Optional sandbox identifier. Uses the zms.businessId property by default */
	string sandboxId = null,
	/** Optional base URL for exposing HTTP API. Uses the com.zetapush.http.baseUrl property by default */
	string baseUrl = null,
	/** Optional additional path */
	string path = null
) {
	if(sandboxId==null) {
		sandboxId = @zms.businessId;
	}
	if(baseUrl==null) {
		baseUrl = @com.zetapush.http.baseUrl;
	}
	var url = str:replace(baseUrl, "${sandboxId}", sandboxId);
	if(path!=null) {
		url = url + path;
	}
	var idx = str:indexOf(url, '://');
	var scheme = str:substr(url, 0, idx+3);
	var location = str:substr(url, idx+3, str:len(url));
	trace("scheme=", scheme, "location=", location);
	url = scheme + str:replace(location, "//", "/");
} return {
	/** The resolved url */
	string url
} on channel __selfName