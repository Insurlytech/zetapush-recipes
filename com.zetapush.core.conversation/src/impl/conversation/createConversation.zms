macroscript core_conversation__createConversation(
	@NotNull string id,
	@NotNull string name,
	@NotNull array members,
	@NotNull string owner,
	@NotNull array groups
) {
	var targets = [];
	/** Create default group for conversation */
	var { result: { group} } = sudo owner call zpRecipeGroup::core_group__createGroupByService({
		id, members, name: str:join('.', 'I18N', 'DEFAULT_GROUP_MEMBERS'), metadata: {}, tags: [], zpService: zpServiceGroups
	}) hardFail;
	/** Add default group to groups list*/
	groups += group;
	/** Insert conversation */
	@@(zpServiceDatabase) INSERT INTO ${CONVERSATION_TABLE} (id, name, createdAt, owner) VALUES (:{id}, :{name}, CURRENT_TIMESTAMP(), :{owner});
	/** Create attachment folder */
	sudo owner zpServiceFileSystem.mkdir({
		folder: CONVERSATION_ATTACHMENTS_PATH + id,
		parents: true
	});
	/** Grant correct permissions for all groups */
	for item in groups {
		/** Add group to broadcast targets */
		targets += item.resource;
		/** Set correct permissions for all groups */
		call core_conversation__setConversationGrants({
			id,
			zpService: item.deploymentId
		}) hardFail;
	}
	/** Execute all registered macros */
	sudo zpRecipeUtils::GLOBAL_OWNER zpRecipeUtils::zpServiceTrigger.trigger({
		event: EVENT__CREATE_CONVERSATION,
		data: { id, name, members, owner, groups }
	});
	/** Store targets */
	sudo owner call zpRecipeUtils::setTargets({ id , targets }) hardFail;
	/** Get created conversation */
	var { result: { details, messages, unread} } = call core_conversation__getConversation({ id, owner }) hardFail;
} broadcast(targets) { details, group, messages, unread } on channel __selfName
